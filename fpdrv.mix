*...+....1....+....2....+....3....+....4....+....5....+....6....+....7....+....8
*LLLLLLLLL OOOO AAAAA             TTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTIIIIIIII
*
*
* FP.TEST DRIVER
*
* INPUT ON CARDS: U, V AND THE RESULTS FOR FADD,FSUB,FMUL,FDIV,FCMP
* IF ONE OF THE RESULTS FAILED, IT OUTPUTS THE SEQUENCE NO, AND THE RESULTS
*
*....1....2....3....4....5....6....7....8....9....0....1....2....3....4....5....
*NPUTn....<-INPUT1-><-INPUT2-><--FADD--><--FSUB--><--FMUL--><--FDIV--><--FCMP-->
*
*
*EQUENCENO....................12345678901234567890123456789012345678901234567890
*
*
* HISTORY
* =======
*
* 250716AP ADDED HARDWARE FP.TESTS
* 250715AP INITIAL REVISION, 1ST WORKING VERSION
*
           ORIG 100
CR         EQU  16
CP         EQU  17
TT         EQU  19
LEN        EQU  16                BUFFER LENGTH
BUF        ORIG *+LEN             INPUT BUFFER
BUF1       ORIG *+LEN             OUTPUT BUFFER
MSG        ALF  SEQNO             TTY BUFFER
           ALF  :    
           ORIG *+12
SEQNO      CON  1
U          ORIG *+1               INPUT U, V
V          ORIG *+1
W          ORIG *+5               W()
RESULT     ORIG *+5               RESULT()
*
START      IN   BUF(CR)           READ CARD
           JBUS *(CR)             WAIT FOR I/O
           ENT3 0                 NFAILED := 0
           LD1  BUF+1(1:1)        LOAD NO.OF INPUTS
           DEC1 30                CONVERT TO NUMBER
           J1Z  9F                ZERO? DONE
           LDA  SEQNO             PRINT SEQNO
           JMP  WTO
           JMP  INPUT             LOAD INPUT DATA
           JMP  STEST             DO SOFT FP.TESTS
*           JMP  HTEST             DO HW FP.TESTS
           J3P  OUTPUT            SAVE RESULTS, IF FAILED
           LDA  SEQNO             SEQNO := SEQNO + 1
           INCA 1
           STA  SEQNO
           JMP  START
9H         HLT  1                 DONE.
*
* WTO - PRINT MSG TO TELETYPE
*
WTO        STJ  9F
           CHAR
           JBUS *(TT)
           STA  MSG+2
           STX  MSG+3
           OUT  MSG(TT)
9H         JMP  *
*
* LOAD INPUT DATA
*
INPUT      STJ  9F
           ENT2 2                 RI2 := 2
           JMP  GETNUM            GET U
           STA  U
           INC2 2                 INC. RI2
           JMP  GETNUM            GET V
           STA  V
           ENT4 4                 SAVE W()
1H         INC2 2                 INC. RI2
           JMP  GETNUM            GET W
           STA  W,4
           DEC4 1
           J4NN 1B
9H         JMP  *
*
* TEST SOFT FP.OPERATIONS
*
STEST      STJ  9F
           ENT4 4                 RI4 := 4
           JMP  SLDAC             INIT ACC, DO FP.ADD
           JMP  FADD
           JMP  SSAVAC            SAVE RESULT, INIT ACC, DO FP.SUB
           JMP  FSUB              DO FP.SUB
           JMP  SSAVAC
           JMP  FMUL              DO FP.MUL
           JMP  SSAVAC
           JMP  FDIV              DO FP.DIV
           JMP  SSAVAC
           JMP  FCMP              DO FP.CMP
           JMP  CVTCI             CONVERT CI TO -1/0/+1
           STA  ACC               NB. WE STORE THE RESULT IN ACC!
           JMP  SSAVAC
9H         JMP  *
*
* TEST HW FP.OPERATIONS
*
HTEST      STJ  9F
           ENT4 4                 RI4 := 4
           JMP  HLDAC             INIT ACC, DO FP.ADD
           FADD V                 DO FP.ADD
           JMP  HSAVAC            SAVE RESULT, INIT ACC, DO FP.SUB
           FSUB V                 DO FP.SUB
           JMP  HSAVAC
           FMUL V                 DO FP.MUL
           JMP  HSAVAC
           FDIV V                 DO FP.DIV
           JMP  HSAVAC
           FCMP V                 DO FP.CMP
           JMP  CVTCI             CONVERT CI TO -1/0/+1
           JMP  HSAVAC
9H         JMP  *
*
* CONVERT CI
*
CVTCI      STJ  9F
           ENTA 1
           JG   9F
           JE   *+2
           DECA 1
           DECA 1
9H         JMP  *

*
* SAVE RESULT, RI4 INDEX TO W()/RESULT()
*
SAV1       CON  0
SAV2       CON  0
SAV3       CON  0
SAV4       CON  0
XACC       CON  0                 XLATED ACC
SLDAC      STJ  9F                ENTRY LOAD
           JMP  8F
SSAVAC     STJ  9F                SAVE RET.ADDRESS
           LD1  SAV1
           LD2  SAV2
           LD3  SAV3
           LD4  SAV4
           LDA  ACC               RA := ACC
           STA  RESULT,4          SAVE TO RESULT()
           JMP  XLATE             CONVERT IT
           STA  XACC              SAVE
           LDA  W,4
           JMP  XLATE
           CMPA XACC
           JE   *+2
           INC3 1                 NFAILED := NFAILED + 1
           DEC4 1                 RI4 := RI4 - 1
8H         LDA  U                 ACC := U
           STA  ACC
           LDA  V
           ST1  SAV1
           ST2  SAV2
           ST3  SAV3
           ST4  SAV4
9H         JMP  *                 RETURN
*
* SAVE RESULT, RI4 INDEX TO W()/RESULT()
*
HLDAC      STJ  9F                ENTRY LOAD
           JMP  8F
HSAVAC     STJ  9F                SAVE RET.ADDRESS
           LD1  SAV1
           LD2  SAV2
           LD3  SAV3
           LD4  SAV4
           STA  RESULT,4          SAVE RA TO RESULT()
           JMP  XLATE             CONVERT IT
           STA  XACC              SAVE
           LDA  W,4
           JMP  XLATE
           CMPA XACC
           JE   *+2
           INC3 1                 NFAILED := NFAILED + 1
           DEC4 1                 RI4 := RI4 - 1
8H         LDA  U                 RA := U
           ST1  SAV1
           ST2  SAV2
           ST3  SAV3
           ST4  SAV4
9H         JMP  *                 RETURN
*
* CONVERT +-0 TO NONZERO WITH SAME SIGN
*
XLATE      STJ  9F
           CMPA =0=
           JNE  9F                NOT ZERO? DONE.
           ENTX 1                 RX := 1
           SRC  1                 RA := SIGN(RA) + 1(1:1)
9H         JMP  *
*
* GET NUMBER FROM CARD
*   RI2 - POINTER TO BUF
*
GETNUM     STJ  9F
           LDA  BUF+1,2(5:5)      OVERPUNCH?
           SUB  2F(0:2)
           STA  TEMP(0:0)         SAVE SIGN
           LDA  BUF,2             LOAD CHARS
           LDX  BUF+1,2
2H         NUM  20                CONVERT
           STA  TEMP(1:5)
           LDA  TEMP              RA := RESULT
9H         JMP  *
*
* SAVE OUTPUT IF FAILED
*
OUTPUT     STJ  9F
           JBUS *(CP)             WAIT FOR I/O
           LDA  SEQNO
           CHAR
           STA  BUF1
           STX  BUF1+1
           ENT2 6                 RI2 := 6, OUTPUT POS
           ENT4 4                 LOOP INDEX := 4
1H         LDA  RESULT,4
           JMP  PUTNUM
           DEC4 1
           J4NN 1B
           OUT  BUF1(CP)          WRITE CARD IMAGE
9H         JMP  *
*
* PUT NUMBER ON CARD
*
PUTNUM     STJ  9F
           CHAR                   CONVERT TO CHAR
           STA  BUF1,2              PRESERVES SIGN!
           STX  BUF1+1,2
           ENTX 1                 +/-1BBBB
           SRC  1
           JANN 1F
           LDA  BUF1+1,2(1:5)
           SUB  =20=
           STA  BUF1+1,2(1:5)
1H         INC2 2
9H         JMP  *
NONZERO    CON  1
