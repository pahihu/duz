*...+....1....+....2....+....3....+....4....+....5....+....6....+....7....+....8
*LLLLLLLLL OOOO AAAAA             TTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTIIIIIIII
*
*
* FP.TEST DRIVER
*
* INPUT ON CARDS: U, V AND THE RESULTS FOR FADD,FSUB,FMUL,FDIV,FCMP
* IF ONE OF THE RESULTS FAILED, IT OUTPUTS THE SEQUENCE NO, AND THE RESULTS
*
*....1....2....3....4....5....6....7....8....9....0....1....2....3....4....5....
*NPUTn....<-INPUT1-><-INPUT2-><--FADD--><--FSUB--><--FMUL--><--FDIV--><--FCMP-->
*
*
*EQUENCENO....................12345678901234567890123456789012345678901234567890
*
           ORIG 100
CR         EQU  16
CP         EQU  17
TT         EQU  19
LEN        EQU  16                BUFFER LENGTH
BUF        ORIG *+LEN             INPUT BUFFER
BUF1       ORIG *+LEN             OUTPUT BUFFER
MSG        ALF  SEQNO             TTY BUFFER
           ALF  :    
           ORIG *+12
SEQNO      CON  1
U          ORIG *+1               INPUT U, V
V          ORIG *+1
W          ORIG *+5               W()
RESULT     ORIG *+5               RESULT()
*
START      IN   BUF(CR)           READ CARD
           JBUS *(CR)             WAIT FOR I/O
           ENT3 1                 NFAILED := 0
           LD1  BUF+1(1:1)        LOAD NO.OF INPUTS
           DEC1 30                CONVERT TO NUMBER
           J1Z  9F                ZERO? DONE
           LDA  SEQNO             PRINT SEQNO
           JMP  WTO
           JMP  INPUT             LOAD INPUT DATA
*           JMP  TEST              DO TESTS
           J3P  OUTPUT            SAVE RESULTS, IF FAILED
           LDA  SEQNO             SEQNO := SEQNO + 1
           INCA 1
           STA  SEQNO
           JMP  START
9H         HLT  1                 DONE.
*
* WTO - PRINT MSG TO TELETYPE
*
WTO        STJ  9F
           CHAR
           JBUS *(TT)
           STA  MSG+2
           STX  MSG+3
           OUT  MSG(TT)
9H         JMP  *
*
* LOAD INPUT DATA
*
INPUT      STJ  9F
           ENT2 0                 RI2 := 0
           JMP  GETNUM            GET U
           STA  U
           INC2 2                 INC. RI2
           JMP  GETNUM            GET V
           STA  V
           ENT4 4                 SAVE W()
1H         INC2 2                 INC. RI2
           JMP  GETNUM            GET W
           STA  W,4
           DEC4 1
           J4NN 1B
9H         JMP  *
*
* TEST FP.OPERATIONS
*
TEST       STJ  9F
           ENT4 4                 RI4 := 4
           JMP  LDAC              INIT ACC, DO FP.ADD
           JMP  FADD
           JMP  SAVAC             SAVE RESULT, INIT ACC, DO FP.SUB
           JMP  FSUB              DO FP.SUB
           JMP  SAVAC
           JMP  FMUL              DO FP.MUL
           JMP  SAVAC
           JMP  FDIV              DO FP.DIV
           JMP  SAVAC
           JMP  FCMP              DO FP.CMP
           JMP  CVTCI             CONVERT CI TO -1/0/+1
           JMP  SAVAC
9H         JMP  *
*
* CONVERT CI
*
CVTCI      STJ  9F
           ENTA 1
           JG   9F
           JE   *+2
           DECA 1
           DECA 1
9H         JMP  *

*
* SAVE RESULT, RI4 INDEX TO W()/RESULT()
*
SAV1       CON  0
SAV2       CON  0
SAV3       CON  0
SAV4       CON  0
SAVAC      STJ  9F                SAVE RET.ADDRESS
           LD1  SAV1
           LD2  SAV2
           LD3  SAV3
           LD4  SAV4
           LDA  ACC               LOAD FP.ACCUMULATOR
           CMPA W,4               COMPARE WITH W()
           JE   *+2
           ENT3 1                 NFAILED := NFAILED + 1
           STA  RESULT,4          SAVE TO RESULT()
           DEC4 1                 RI4 := RI4 - 1
LDAC       STJ  9F                ENTRY LOAD
           LDA  U                 ACC := U
           STA  ACC
           LDA  V
           ST1  SAV1
           ST2  SAV2
           ST3  SAV3
           ST4  SAV4
9H         JMP  *                 RETURN
*
* GET NUMBER FROM CARD
*   RI2 - POINTER TO BUF
*
GETNUM     STJ  9F
           LDA  BUF+4,2(5:5)      OVERPUNCH?
           SUB  2F(0:2)
           STA  TEMP(0:0)         SAVE SIGN
           LDA  BUF+4,2           LOAD CHARS
           LDX  BUF+5,2 
2H         NUM  20                CONVERT
           STA  TEMP(1:5)
           LDA  TEMP              RA := RESULT
9H         JMP  *
*
* SAVE OUTPUT IF FAILED
*
OUTPUT     STJ  9F
           JBUS *(CP)             WAIT FOR I/O
           LDA  SEQNO
           CHAR
           STA  BUF1
           STX  BUF1+1
           ENT2 6                 RI2 := 6, OUTPUT POS
           ENT4 4                 LOOP INDEX := 4
* 1H         LDA  RESULT,4
1H         LDA  W,4
           JMP  PUTNUM
           DEC4 1
           J4NN 1B
           OUT  BUF1(CP)          WRITE CARD IMAGE
9H         JMP  *
*
* PUT NUMBER ON CARD
*
PUTNUM     STJ  9F
           STA  NONZERO(0:0)      SAVE SIGN
           CHAR                   CONVERT TO CHAR
           STA  BUF1,2
           STX  BUF1+1,2
           LDA  NONZERO
           JANN 1F
           LDA  BUF1+1,2
           SUB  =20=
           STA  BUF1+1,2
1H         INC2 2
9H         JMP  *
NONZERO    CON  1
